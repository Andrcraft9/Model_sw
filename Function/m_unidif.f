
C======================================================================
C OCEAN GENERAL CIRCULATION MODEL SUBROUTINES.
C UNIVERSAL DIFFUSION: 1)ALONG SIGMA LEVELS; 2)ALONG HORIZONTAL LEVELS;
C                      3)ALONG ISOPICNAL AND THEIR COMBINATION.
C DIANSKY N.A.  23.05.99 13:29
C=====================================================================
      SUBROUTINE SLRO(DEN,SLRX,SLRY)
      IMPLICIT NONE
C CALCULATING SLOPE FOR ISOPICNAL DIFFUSION
C DEN0 -- SURFACE DENSITY
C DEN  -- DENSITY
C (SLRX,SLRY) -- HH*(Dx(DENSITY),Dy(DENSITY))/(dDENSITY/dZ)
C              - ISOPICNAL SLOPES ON C-GRIDS
C EPSRHO -- MIN VERTICAL DENSITY GRADIENT (COMMONLY 1.0E-05)
C PERIODICITY IS TAKEN TO ACCOUNT BY MASKS LU, LCU AND LCV
C VERTICAL DENSITY GRADIENT IS AGREEMENT WITH THE OPERATOR OF
c ISOPICNAL DIFFUSION
      INCLUDE '0COM.INC'
C EPSRHO -- MIN VERTICAL DENSITY GRADIENT (COMMONLY 1.0E-12)
      REAL      EPSRHO, RG
      PARAMETER(EPSRHO=1.0E-09,RG=1.0E-07*GRV*RH0)

      REAL DEN(NX,NY,NZ),SLRX(NX,NY,NZ),SLRY(NX,NY,NZ)
      INTEGER M,N,K
      REAL RHOZ

C DEFINE ARRAYS ON DEPTH LEVELS

!$OMP PARALLEL DO PRIVATE(M,N,K,RHOZ)
      DO K=2,NZ
         DO N=NNN,NN
            DO M=MMM,MM
               IF(LCU(M,N).GT.0.5) THEN
C VERTICAL DENSITY GRADIENT ON XC-W-GRID AS MEAN ON T-POINTS
C P-GRADIENT IS ADDED ACCORDING TO BRYDEN FORMULA (VARIANT 2)

      RHOZ=(DEN(M,N,K)-DEN(M,N,K-1)+DEN(M+1,N,K)-DEN(M+1,N,K-1))/
     &                                   (2.0*HHU(M,N)*HZT(K))
c     +   + RG*(5.07043E-04-5.43283E-07*RG*2.0*HHU(M,N)* ZW(K))

            IF(RHOZ.LT.EPSRHO) RHOZ=EPSRHO

C  ISOPICNAL SLOPE ON XC-W-GRID AS MEAN ON T-POINTS
            SLRX(M,N,K)=( DEN(M+1,N,K-1)-DEN(M,N,K-1)
     &                   +DEN(M+1,N,K  )-DEN(M,N,K  ) )/(2.0*RHOZ)

               END IF
               IF(LCV(M,N).GT.0.5) THEN
C  VERTICAL DENSITY GRADIENT ON YC-W-GRID AS MEAN ON T-POINTS
C P-GRADIENT IS ADDED ACCORDING TO BRYDEN FORMULA (VARIANT 2)

      RHOZ=(DEN(M,N,K)-DEN(M,N,K-1)+DEN(M,N+1,K)-DEN(M,N+1,K-1))/
     &                                   (2.0*HHV(M,N)*HZT(K))
c     +   + RG*(5.07043E-04-5.43283E-07*RG*2.0*HHV(M,N)* ZW(K))

            IF(RHOZ.LT.EPSRHO) RHOZ=EPSRHO

C  ISOPICNAL SLOPE ON YC-W-GRID AS MEAN ON T-POINTS
            SLRY(M,N,K)=( DEN(M,N+1,K-1)-DEN(M,N,K-1)
     &                   +DEN(M,N+1,K  )-DEN(M,N,K  ) )/(2.0*RHOZ)

               END IF
            END DO
         END DO
      END DO
!$OMP END PARALLEL DO
      
	IF(MMD.NE.0) THEN
	 CALL CYCLIZE(SLRX,NX,NY,NZ,MMM,MM)
	 CALL CYCLIZE(SLRY,NX,NY,NZ,MMM,MM)      
      END IF

      RETURN
      END
C=======================================================================
      SUBROUTINE TRDTXZ(FF,SLRX,AFNT, AFNV,UU,AMX,TAU,IFLAG,
     &                             AS,AZ,AR,SLH,SLH0)
      IMPLICIT NONE
C FOR TRANSPORT AND DIFFUSION OF PASSIVE COMPONENT IN X - DIRECTION
C MODIFIED BY DIANSKII N.A. 01.05.99 19:12
C UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG SIGMA,
C HORIZONTAL LEVELS AND ISOPICNALS WITH AS,AZ,AR COEFFICIENTS
C X-TRANSPORT AND DIFFUSION.
C X-TRANSPORT SUPPOSES BOUNDARY VELOCITIES & FI TO BE ZERO.
C FF -- PASSIVE TRACER
C UU -- VELOCITY IN X - DIRECTION
C SLRX -- HH*Dx(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN X-DIRECTION
C FI -- STREAM FUNCTION FOR ADDING BAROTROPIC VELOCITY
C AMX -- COEFFICIENT OF DIFFUSION IN X-DIRECTION
C TAU -- TIME STEP
C IGRX-- DIRIHLET OR NEIMANN BOUNDARY CONDITION
C IFLAG -- CONTROL OF KIND OF DIFFUSION AND DIRECTION FOR K-LOOP
C          IF IFLAG>0 THEN K=1,NZ,+1
C          IF IFLAG<0 THEN K=NZ,1,-1
C AS -- WEIGHT COEFFICIENT FOR DIFFUSION ALONG SIGMA-LEVELS
C AZ -- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS
C AR -- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS
C IF:AS=1.0 AND AZ=AR=0.0 THEN DIFFUSION ALONG SIGMA LEVELS
C    AS=AZ=1.0 AND AR=0.0 THEN DIFFUSION ALONG HORIZONTAL LEVELS
C    AS=AR=1.0 AND AZ=0.0 THEN DIFFUSION ALONG ISOPICNALS
      INCLUDE '0COM.INC'
C IMPLICIT - EXPLICIT CONTROL TRANSPORT T & S TIME SCHEME:
      INCLUDE '0TRTSS.INC'

      REAL FF(NX,NY,NZ),UU(NX,NY,NZ),SLRX(NX,NY,NZ),
     &     AMX(NX, NY, NZ), TAU, AS, AZ, AR
      REAL    AFNT(NX,NY,NZ+1), AFNV(NX,NY,NZ+1)
C SWEEP VARIABLES
      REAL A(NX),B(NX),C(NX),ETA(NX),RKSI(NX)
C ADDITIONAL VARIABLES:
      REAL AMXX(NX,NZ+1),AMXZ(NX,NZ+1)
	REAL(8) SLH(NX,NY),SLH0(NX,NY)

C SWEEP BOUNDARIES
      INTEGER II,JJ,IFLAG,KBEG,KEND,KSTP,M,N,K,M4,M6,NSWEEP, MLOOP
C HELP VARIABLES FOR INTERNAL CALCULATIONS
      REAL    BP,BP0,DP,DM,PP,PM,DC1,AMUW,ALFR,ALFZ
      REAL    AFUNT, AFUNV, AFUNTP1, AFUNVP1
      INTEGER KFM1, KFP1 
C-------------------------------------------------------------------
C      UUC  = 1.0/HX/2.0                  !MAIN VELOSITY COEFFICIENT
C  BEGIN MAIN ARRAY LOOP
C    BOUNDARY CONTIONS ON TOP AND BOTTOM

             AMXX  =0.0
             AMXZ  =0.0

C         TO FLIP OR NOT DIRECTION OF DO CICLE ON Z
                    IF(IFLAG.GT.0) THEN
                        KBEG=1
                        KEND=NZ
                        KSTP=1
                     ELSE
                        KBEG=NZ
                        KEND=1
                        KSTP=-1
                     END IF

!$OMP PARALLEL DO 
!$OMP&PRIVATE(K,M,N,AFUNT,AFUNV,AFUNTP1,AFUNVP1,ALFR,ALFZ,AMUW,
!$OMP&     DC1,KFM1,KFP1,NSWEEP,II,JJ,MLOOP,M4,M6,
!$OMP&     BP,BP0,DM,DP,PM,PP,A,B,C,ETA,RKSI)
!$OMP&FIRSTPRIVATE(AMXX,AMXZ)
      DO N=NNN,NN
C         DC0  = RM(N)/(4.0*HX*HX)        !MAIN DIFUSUION  COEFFICIENT
C         TDC  =   1./(TAU*RM(N))         !TIME DERIVATIVE COEFFICIENT

C  DEFINITION OF COEFFICIENTS FOR DIFFERENCE SHEMS
C  CONTROL FOR BOUNDARY CONDITION MUST BE HERE
C  BOUNDARY CONTIONS ON LEFT AND RIGHT OF SWEEP ARE SATISFIED BY LCU MASK
C  BOUNDARY VALUES FOR PERIODIC CASE ARE SATISFIED BY PERIODICITY OF ARRAYS

C  UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG SIGMA,
C  HORIZONTAL LEVELS AND ISOPICNALS
         DO K=2,NZ
            DO M=MMM-1,MM
C  DEFINITION OF COEFFICIENTS FOR FINITE DIFFERENCE SCHEME IN C-GRID POINTS
            IF(LCU(M,N).GT.0.5) THEN
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION

             AFUNT = AFNT(M,N,K)
             AFUNV = AFNV(M,N,K)
             
             AFUNTP1 = AFNT(M+1,N,K)
             AFUNVP1 = AFNV(M+1,N,K)
  
             ALFR=0.5*(AFUNT+AFUNTP1)
C  COEF. OF DIFFUSION IN W-POINTS AS FUNCTION ON Z:
             AMUW=0.25*(AMX(M,N,K-1)+AMX(M,N,K))*(AFUNV+AFUNVP1)
     &                *DYH(M,N)/DXT(M,N)/RN
             ALFZ=(1.0-ALFR)*AR+AZ
             ALFR=ALFR*AR
C  DIFFUSION ALONG SIGMA LEVELS:
C            AMXX(M,K)=AS*AMUW*HZT(K)*0.5*(HH (M,N-1)+HH (M,N ))
             AMXX(M,K)=AS*AMUW*HZT(K)*HHU(M,N)
C  ADDITIONAL TERM FOR DIFFUSION ALONG ISOPICNALS AND HORIZONTAL LEVELS:
             AMXZ(M,K)=AMUW*(ALFR*SLRX(M,N,K)+
C     &              ALFZ*ZW(K) *(HHQ(M+1,N)-HHQ(M,N )))
     &              ALFZ*(Z3D(M+1,N,K  )-Z3D(M,N,K  )
     &                   +Z3D(M+1,N,K-1)-Z3D(M,N,K-1))*0.5)
            ELSE
             AMXX(M,K)=0.0E+00
             AMXZ(M,K)=0.0E+00
            END IF

            END DO
         END DO


C  BLOCK FOR X-Z PART OF CROSSDIFUUSION OPERATOR

            DO K=KBEG,KEND,KSTP

C  CHANGE DIFFERENCE APPROXIMATION AND INDEX FOR FF()
C                                ON THE SURFACE AND BOTTOM BOUNDARIES
            IF(K.EQ.1.OR.K.EQ.NZ)  THEN
               DC1 = 0.5/DZ(K)
            ELSE
               DC1 = 0.25/DZ(K)
            END IF

            KFM1=MAX(1,K-1)
            KFP1=MIN(NZ,K+1)
C   IN ABOVE "IF-THEN-ELSE" THE "1/DZ(K)" MAY BE CHANGED FOR
C   SECOND INDEX OF APPROXIMATION BY APPROPRIATE COMBINATION

                        DO  NSWEEP=1,ABS(LRX(N))

         II = IIX(NSWEEP,N)
         JJ = JJX(NSWEEP,N)
         IF(JJ.GT.MM) JJ=JJ-MMD     !DISCONTINUOUS SWEEP LINE
           DO MLOOP=II,JJX(NSWEEP,N)
                 M  = MLOOP
                 M4 = MLOOP - 1
                 M6 = MLOOP + 1
                 IF ( M.GT.MM) M  = M  - MMD
                 IF (M4.GT.MM) M4 = M4 - MMD
                 IF (M6.GT.MM) M6 = M6 - MMD

C            BP = TDC*HHQ(M,N)          !TIME DERIVATIVE COEFFICIENT
            BP = (HHQ(M,N)-SNGL(SLH (M,N)))*DX(M,N)*DY(M,N)*RN / TAU
            BP0= (HHQ(M,N)-SNGL(SLH0(M,N)))*DX(M,N)*DY(M,N)*RN / TAU

C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMXZ
            DM = DC1*(AMXX(M4,K)+AMXX(M4,K+1)         !DIFFUSION
     +               +AMXZ(M4,K)-AMXZ(M4,K+1))        !CROSS DIFFUSION
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMXZ
            DP = DC1*(AMXX(M,K)+AMXX(M,K+1)             !DIFFUSION
     -               -AMXZ(M,K)+AMXZ(M,K+1))            !CROSS DIFFUSION
C            PP = ( UU(M ,N,K)*(HH(M,N)+HH(M,N-1))/2.
C     &            )*UUC
C            PM = ( UU(M4,N,K)*(HH(M4,N)+HH(M4,N-1))/2.
C     &            )*UUC
          PP = UU(M ,N,K)*DYH(M ,N)
     &       *HHU(M ,N)/2.0

          PM = UU(M4,N,K)*DYH(M4,N)
     &       *HHU(M4,N)/2.0
C LAST "2" ABOVE IS DUE TO APROXIMATION IN SPACE.
C  FIRST SWEEP COEFFICIENT:
          A(M) = -PM * TRTS_IMP - DM
C  THIRD SWEEP COEFFICIENT:
          C(M) =  PP * TRTS_IMP - DP
C  SECOND(CENTRAL) SWEEP COEFFICIENT:
          B(M) =  BP
     +            + DC1*( AMXX(M4,K)+AMXX(M4,K+1)
     +                   +AMXX(M ,K)+AMXX(M ,K+1)
     -                   -AMXZ(M4,K)+AMXZ(M4,K+1)
     +                   +AMXZ(M ,K)-AMXZ(M ,K+1) )

C  RIGHT HAND PART OF SWEEP (FULL CASE):
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMXZ FROM CENTRAL (M) TERMS
          ETA(M) = BP0* FF(M,N,K) - PP * TRTS_EXP * FF(M6,N,K)
     +                            + PM * TRTS_EXP * FF(M4,N,K)
     +           +  DC1*(
     +   +FF(M4,N,KFM1)*( AMXX(M4,K  )-AMXZ(M4,K))
     +   +FF(M ,N,KFM1)*(-AMXX(M4,K  )-AMXX(M ,K)
     +                   -AMXZ(M4,K  )+AMXZ(M ,K))
     +   +FF(M6,N,KFM1)*( AMXX(M ,K  )+AMXZ(M ,K))
     +   +FF(M4,N,KFP1)*( AMXX(M4,K+1)+AMXZ(M4,K+1))
     +   +FF(M ,N,KFP1)*(-AMXX(M4,K+1)-AMXX(M ,K+1)
     +                   +AMXZ(M4,K+1)-AMXZ(M ,K+1))
     +   +FF(M6,N,KFP1)*( AMXX(M ,K+1)-AMXZ(M ,K+1)))

         END DO                       !END OF M-LOOP X-Z DIRECTION

C IMPLICIT: A(M)=-DM-PM;C(M)=-DP+PP;B(M)=BP+DM+DP;ETA(M)=BP*FF(M,N,K)
C IF (M=II) : PM = 0, A = -DM ; IF (M=JJ) : PP = 0, C = -DP.
C IF(IGRX.EQ.1) : POINTS OUTSIDE T-GRID IS TAKEN, SO THEY MUST BE = 0,
C OR U', FI = 0.
         IF (LRX(N).GT.0) THEN
          CALL FACTOR2(NX,A,B,C,ETA,RKSI,II,JJX(NSWEEP,N))
         ELSE
          CALL FACTORC(A,B,C,ETA,RKSI,MMM,MM)
         ENDIF

         DO MLOOP=II,JJX(NSWEEP,N)
          M  = MLOOP
          IF (M.GT.MM) M  = M  - MMD
          FF(M,N,K) = RKSI(M)
         ENDDO

                            END DO    !END OF NSWEEP-LOOP X-Z DIRECTION
         END DO                       !END OF K-LOOP
C  END OF BLOCK FOR X-Z PART OF CROSSDIFUUSION OPERATOR

      END DO                          !END OF N-LOOP
!$OMP END PARALLEL DO

      RETURN
      END
C=====================================================================72
      SUBROUTINE TRDTZXY(FF,SLRX,SLRY,AFNT,AFNV,
     &                   AMX,AMY,TAU,IFLAG,AZ,AR,BZ,BR,BZR)
      IMPLICIT NONE
C FOR TRANSPORT AND DIFFUSION OF PASSIVE COMPONENT
C UNIVERSAL COMPLEX PART OF DIFFUSION IN Z-DIRECTION AS SUMM OF
C DIFFUSION ALONG HORIZONTAL LEVELS AND ISOPICNALS
C MODIFIED BY DIANSKII N.A. 01.05.99 19:17
C X-TRANSPORT AND DIFFUSION.
C X-TRANSPORT SUPPOSES BOUNDARY VELOCITIES & FI TO BE ZERO.
C FF -- PASSIVE TRACER
C SLRX -- HH*Dx(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN X-DIRECTION
C AMX -- COEFFICIENT OF DIFFUSION IN X-DIRECTION
C SLRY -- HH*Dy(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN Y-DIRECTION
C AMY -- COEFFICIENT OF DIFFUSION IN Y-DIRECTION
C TAU -- TIME STEP
C IFLAG -- CONTROL OF KIND OF DIFFUSION AND DIRECTION FOR K-LOOP
C          IF IFLAG>0 THEN M=MMM,MM ,+1;N=NNN,NN, +1 FOR MAIN N-LOOP
C          IF IFLAG<0 THEN M=MM ,MMM,-1;N=NN ,NNN,-1 FOR MAIN N-LOOP
C AZ- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS]IN FIRST
C AR- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS       ]   PART
C BZ- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS}
C BR- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS       }IN SECOND
C BZR-WEIGHT COEFFICIENT FOR MIXT DIFFUSION ALONG HORIZONTAL  }   PART
C                                      LEVELS AND ISOPICNALS  }
C IF:AZ=AR=BZ=BR=BZR=0.0         THEN DIFFUSION ALONG SIGMA LEVELS
C    AZ=BZ=1.0 AND AR=BR=BZR=0.0 THEN DIFFUSION ALONG HORIZONTAL LEVELS
C    AR=BR=1.0 AND AZ=BZ=BZR=0.0 THEN DIFFUSION ALONG ISOPICNALS
C    AR=BZR=1.0 AND AZ=BZ=BR=0.0 THEN ISOPICNAL DIFFUSION ALONG
C                                             HORIZONTAL LEVELS

      INCLUDE '0COM.INC'
C IMPLICIT - EXPLICIT CONTROL TRANSPORT T & S TIME SCHEME:
      INCLUDE '0TRTSS.INC'
      REAL  BZ, BZR
      REAL  FF(NX,NY,NZ),SLRX(NX,NY,NZ),AMX(NX,NY,NZ),
     &                   SLRY(NX,NY,NZ),AMY(NX,NY,NZ),TAU,AZ,AR
      REAL    AFNT(NX,NY,NZ+1), AFNV(NX,NY,NZ+1)
C SWEEP VARIABLES
      REAL A(NZ),B(NZ),C(NZ),ETA(NZ),RKSI(NZ)
C ADDITIONAL VARIABLES:
      REAL, ALLOCATABLE:: AMZX(:,:)  , AMZZ(:,:),
     &                    AMZY(:,:,:), BMZZ(:,:,:)

C     REAL AMZX(NX,NZ+1)   ,AMZZ(NX,NZ+1),
C    &     AMZY(NY,NZ+1,NX),BMZZ(NY,NZ+1,NX)
C AMZY(NY,NZ+1,NX) -- ARRAY FOR COEFFICIENT OF DIFFUSIVE FLUXES
C                     IN Y-DIRECTION

C SWEEP BOUNDARIES
      INTEGER IFLAG,MBEG,MEND,MSTP,M,N,K
C HELP VARIABLES FOR INTERNAL CALCULATIONS
      REAL DCX1,DCX2,TDC,AMUW,HLAMZ,HLAMP,ALFR,ALFZ
      REAL AFUNT, AFUNV, AFUNTP1, AFUNVP1, DCY1, DCY2, BLFR, BR
      REAL BLFZR, BLFZ
      INTEGER KFM1, KFP1, NEND, NSTP, NBEG
C--------------------------R O O L--------------------------------------

C ALLOCATE NEED ADDITIONAL VARIABLES:  	
	ALLOCATE(AMZX(NX,NZ+1)   ,AMZZ(NX,NZ+1),
     &           AMZY(NY,NZ+1,NX),BMZZ(NY,NZ+1,NX))


C  DEFINE ARRAY OF COEFFICIENTS FOR Z-Y DIRECTION
      DO M=MMM-1,MM

C  DEFINITION OF COEFFICIENTS FOR DIFFERENCE SHEMS
C  BOUNDARY CONTIONS ON LEFT AND RIGHT OF SWEEP ARE SATISFIED BY LCV MASK
C  DEFINITION IN INNER DOMAIN

C  UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG
C  HORIZONTAL AND ISOPICNALS LEVELS.
C  ADDITIONAL TERM IN Z-DIRECTION FOR DIFFUSION ALONG
C  HORIZONTAL LEVELS AND ISOPICNALS ARISEN FOR Y-DIRECTION:
         DO K=2,NZ
            DO N=NNN-1,NN
C  DEFINITION OF COEFFICIENTS FOR FINITE DIFFERENCE SCHEME IN C-GRID POINTS
            IF(LCV(M,N).GT.0.5) THEN
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION
             AFUNT = AFNT(M,N,K)
             AFUNV = AFNV(M,N,K)
              
             AFUNTP1 = AFNT(M,N+1,K)
             AFUNVP1 = AFNV(M,N+1,K)

             ALFR=0.5*(AFUNT+AFUNTP1)
C  COEF. OF DIFFUSION IN W-POINTS AS FUNCTION ON Z:
             AMUW=0.25*(AMY(M,N,K-1)+AMY(M,N,K))
     &                *DXH(M,N)/DYT(M,N)/RN                    
     &                *(AFUNV+AFUNVP1)
               ALFZ=(1.0-ALFR)*AR+AZ
               BLFR=BR*ALFR
               BLFZ=(1.0-ALFR)*(BR+BZR)+BZ
              BLFZR=ALFR*BZR
               ALFR=ALFR*AR
               HLAMZ=    (Z3D(M,N+1,K  )-Z3D(M,N,K  )
     &                   +Z3D(M,N+1,K-1)-Z3D(M,N,K-1))*0.5
C               HLAMZ=ZW(K)*(HHQ(M,N+1)-HHQ(M,N))    !HORIZONTAL
               HLAMP=SLRY(M,N,K)                    !ISOPICNAL
               AMZY(N,K,M)=AMUW*(ALFZ*HLAMZ   +ALFR*HLAMP)
               BMZZ(N,K,M)=AMUW*(BLFZ*HLAMZ**2+BLFR*HLAMP**2
     +                         +BLFZR*HLAMZ   *     HLAMP)/
C    /                  (0.5*(HH (M-1,N)+HH (M,N))*HZT(K))
     /                  (HHV(M,N)*HZT(K))
            ELSE
               AMZY(N,K,M)=0.0E+00
               BMZZ(N,K,M)=0.0E+00
            END IF
            END DO
         END DO
C    BOUNDARY CONTIONS ON TOP AND BOTTOM
            DO N=NNN-1,NN
               AMZY(N,1,M)   =0.0E+00
               AMZY(N,NZ+1,M)=0.0E+00
               BMZZ(N,1,M)   =0.0E+00
               BMZZ(N,NZ+1,M)=0.0E+00
            END DO
      END DO

C  BEGIN MAIN OPERATION LOOP

C  FLIP OR NOT DIRECTION OF DO-LOOP IN Y AND X DIRECTIONS
           IF(IFLAG.GT.0) THEN
              MBEG=MMM
              MEND=MM
              MSTP= 1
                  NBEG=NNN
                  NEND=NN
                  NSTP=1
           ELSE
              MBEG=MM
              MEND=MMM
              MSTP=-1
                  NBEG=NN
                  NEND=NNN
                  NSTP=-1
           END IF

C         DCY0  = 1.0/(4.0*HY*HY)       !MAIN Y-DIFUSUION COEFFICIENT
      DO N=NBEG,NEND,NSTP
C         DCX0  = RM(N)/(4.0*HX*HX)     !MAIN X-DIFUSUION COEFFICIENT

C  DEFINITION OF COEFFICIENTS FOR DIFFERENCE SHEMS
C  BOUNDARY CONTIONS ON LEFT AND RIGHT OF SWEEP ARE SATISFIED BY LCU MASK
C  BOUNDARY VALUES FOR PERIODIC CASE ARE SATISFIED BY PERIODICITY OF ARRAYS

C  UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG
C  HORIZONTAL AND ISOPICNALS LEVELS.
C  ADDITIONAL TERM IN Z-DIRECTION FOR DIFFUSION ALONG
C  HORIZONTAL LEVELS AND ISOPICNALS ARISEN FOR X-DIRECTION:
         DO K=2,NZ
            DO M=MMM-1,MM
C  DEFINITION OF COEFFICIENTS FOR FINITE DIFFERENCE SCHEME IN C-GRID POINTS
            IF(LCU(M,N).GT.0.5) THEN
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION

             AFUNT = AFNT(M,N,K)
             AFUNV = AFNV(M,N,K)

             AFUNTP1 = AFNT(M+1,N,K)
             AFUNVP1 = AFNV(M+1,N,K)

             ALFR=0.5*(AFUNT+AFUNTP1)
C  COEF. OF DIFFUSION IN W-POINTS AS FUNCTION ON Z:
             AMUW=0.25*(AMX(M,N,K-1)+AMX(M,N,K))*(AFUNV+AFUNVP1)
     &                *DYH(M,N)/DXT(M,N)/RN
               ALFZ=(1.0-ALFR)*AR+AZ
               BLFR=BR*ALFR
               BLFZ=(1.0-ALFR)*(BR+BZR)+BZ
              BLFZR=ALFR*BZR
               ALFR=ALFR*AR
C               HLAMZ=ZW(K)*(HHQ(M+1,N)- HHQ(M,N))    !HORIZONTAL
               HLAMZ=    (Z3D(M+1,N,K  )-Z3D(M,N,K  )
     &                   +Z3D(M+1,N,K-1)-Z3D(M,N,K-1))*0.5
               HLAMP=SLRX(M,N,K)                     !ISOPICNAL
               AMZX(M,K)=AMUW*(ALFZ*HLAMZ   +ALFR*HLAMP)
               AMZZ(M,K)=AMUW*(BLFZ*HLAMZ**2+BLFR*HLAMP**2+
     +                        BLFZR*HLAMZ   *     HLAMP)/
C     /                        (0.5*(HH (M,N-1)+HH (M,N))*HZT(K))
     /                        (HHU(M,N)*HZT(K))
            ELSE
               AMZX(M,K)=0.0E+00
               AMZZ(M,K)=0.0E+00
            END IF
            END DO
         END DO
C    BOUNDARY CONTIONS ON TOP AND BOTTOM
            DO M=MMM-1,MM
               AMZX(M,1)   =0.0
               AMZX(M,NZ+1)=0.0
               AMZZ(M,1)   =0.0
               AMZZ(M,NZ+1)=0.0
            END DO


C  BLOCK FOR Z-X PART OF CROSSDIFUUSION OPERATOR


         DO M=MBEG,MEND,MSTP
         IF (LU(M,N).GT.0.5) THEN

            TDC  =  HHQ(M,N)*DX(M,N)*DY(M,N)*RN / TAU     !TIME DERIATIVE COEFFICIENT

C  CHANGE DIFFERENCE APPROXIMATION ON THE BOUNDARIES IN X-DIRECTION
            IF(LCU(M-1,N).LT.0.5.OR.LCU(M,N).LT.0.5) THEN
               DCX1 = 0.5
            ELSE
               DCX1 = 0.25
            END IF
C  CHANGE DIFFERENCE APPROXIMATION ON THE BOUNDARIES IN Y-DIRECTION
            IF(LCV(M,N-1).LT.0.5.OR.LCV(M,N).LT.0.5)  THEN
               DCY1 = 0.5
            ELSE
               DCY1 = 0.25
            END IF

            DO  K=1,NZ

C  CHANGE INDEX FOR FF() ON THE SURFACE AND BOTTOM BOUNDARIES
            IF(K.EQ.1)  THEN
               KFM1=1
               KFP1=K+1
            ELSEIF(K.LT.NZ) THEN
               KFM1=K-1
               KFP1=K+1
            ELSE
               KFM1=K-1
               KFP1=NZ
            END IF

               DCX2 = DCX1/DZ(K)
               DCY2 = DCY1/DZ(K)

C   IN ABOVE OPERAND THE "1/HZT(K)" MAY BE CHANGED FOR
C   SECOND INDEX OF APPROXIMATION BY APPROPRIATE COMBINATION

C  FIRST  SWEEP COEFFICIENT:
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX AND AMZY
            A(K) = DCX2*(-AMZZ(M-1,K)  -AMZZ(M  ,K)
     +                   +AMZX(M  ,K)  -AMZX(M-1,K))
     +           + DCY2*(-BMZZ(N-1,K,M)-BMZZ(N  ,K,M)
     +                   +AMZY(N  ,K,M)-AMZY(N-1,K,M))

C  SECOND(CENTRAL) SWEEP COEFFICIENT:
            B(K) = TDC+DCX2*(
     +        -AMZX(M,K+1)  +AMZX(M,K)  +AMZX(M-1,K+1)  -AMZX(M-1,K)
     +        +AMZZ(M,K+1)  +AMZZ(M,K)  +AMZZ(M-1,K+1)  +AMZZ(M-1,K))
     +        +        DCY2*(
     +        -AMZY(N,K+1,M)+AMZY(N,K,M)+AMZY(N-1,K+1,M)-AMZY(N-1,K,M)
     +        +BMZZ(N,K+1,M)+BMZZ(N,K,M)+BMZZ(N-1,K+1,M)+BMZZ(N-1,K,M))

C  THIRD SWEEP COEFFICIENT:
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX AND AMZY
            C(K) = DCX2*(-AMZZ(M-1,K+1)  -AMZZ(M  ,K+1)
     +                   +AMZX(M-1,K+1)  -AMZX(M  ,K+1))
     +           + DCY2*(-BMZZ(N-1,K+1,M)-BMZZ(N  ,K+1,M)
     +                   +AMZY(N-1,K+1,M)-AMZY(N  ,K+1,M))

C  RIGHT HAND PART OF SWEEP (FULL CASE):
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX FROM CENTRAL (K) TERMS
          ETA(K) =  TDC*FF(M,N,K)-DCX2*(
     +   +FF(M-1,N,KFM1)*( AMZX(M-1,K  )-AMZZ(M-1,K))
     +   +FF(M-1,N,K   )*(+AMZZ(M-1,K+1)+AMZZ(M-1,K)
     +                    -AMZX(M-1,K+1)+AMZX(M-1,K))
     +   +FF(M-1,N,KFP1)*(-AMZX(M-1,K+1)-AMZZ(M-1,K+1))
     +   +FF(M+1,N,KFM1)*(-AMZX(M ,K  ) -AMZZ(M ,K  ))
     +   +FF(M+1,N,K   )*(+AMZZ(M ,K  ) +AMZZ(M ,K+1)
     +                    -AMZX(M ,K  ) +AMZX(M ,K+1))
     +   +FF(M+1,N,KFP1)*( AMZX(M ,K+1) -AMZZ(M ,K+1)))
     +                           -DCY2*(
     +   +FF(M,N-1,KFM1)*( AMZY(N-1,K  ,M)-BMZZ(N-1,K  ,M))
     +   +FF(M,N-1,K   )*(+BMZZ(N-1,K+1,M)+BMZZ(N-1,K  ,M)
     +                    -AMZY(N-1,K+1,M)+AMZY(N-1,K  ,M))
     +   +FF(M,N-1,KFP1)*(-AMZY(N-1,K+1,M)-BMZZ(N-1,K+1,M))
     +   +FF(M,N+1,KFM1)*(-AMZY(N  ,K  ,M)-BMZZ(N  ,K  ,M))
     +   +FF(M,N+1,K   )*(+BMZZ(N  ,K  ,M)+BMZZ(N  ,K+1,M)
     +                    -AMZY(N  ,K  ,M)+AMZY(N  ,K+1,M))
     +   +FF(M,N+1,KFP1)*( AMZY(N  ,K+1,M)-BMZZ(N  ,K+1,M)))


            END DO                       !END OF K-LOOP


         CALL FACTOR(NZ,A,B,C,ETA,RKSI,1,NZ)
            DO K=1,NZ
               FF(M,N,K) = RKSI(K)
            ENDDO

         ENDIF
         END DO                    !END OF M-LOOP

C  END OF BLOCK FOR Z-X PART OF CROSSDIFUUSION OPERATOR
      END DO                       !END OF N-LOOP


C DEALLOCATE NEED ADDITIONAL VARIABLES:  	
	DEALLOCATE(BMZZ,AMZY,AMZZ,AMZX)

      RETURN
      END
C=====================================================================72
      SUBROUTINE TRDTZX(FF,SLRX,AFNT,AFNV,
     &                  AMX,TAU,IFLAG,AZ,AR,BZ,BR,BZR,SLH,SLH0)
      IMPLICIT NONE
C FOR TRANSPORT AND DIFFUSION OF PASSIVE COMPONENT
C UNIVERSAL COMPLEX PART OF DIFFUSION IN Z-DIRECTION AS SUMM OF
C DIFFUSION ALONG HORIZONTAL LEVELS AND ISOPICNALS
C MODIFIED BY DIANSKII N.A. 01.05.99 19:17
C X-TRANSPORT AND DIFFUSION.
C X-TRANSPORT SUPPOSES BOUNDARY VELOCITIES & FI TO BE ZERO.
C FF -- PASSIVE TRACER
C SLRX -- HH*Dx(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN X-DIRECTION
C AMX -- COEFFICIENT OF DIFFUSION IN X-DIRECTION
C SLRY -- HH*Dy(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN Y-DIRECTION
C AMY -- COEFFICIENT OF DIFFUSION IN Y-DIRECTION
C TAU -- TIME STEP
C IFLAG -- CONTROL OF KIND OF DIFFUSION AND DIRECTION FOR K-LOOP
C          IF IFLAG>0 THEN M=MMM,MM ,+1;N=NNN,NN, +1 FOR MAIN N-LOOP
C          IF IFLAG<0 THEN M=MM ,MMM,-1;N=NN ,NNN,-1 FOR MAIN N-LOOP
C AZ- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS]IN FIRST
C AR- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS       ]   PART
C BZ- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS}
C BR- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS       }IN SECOND
C BZR-WEIGHT COEFFICIENT FOR MIXT DIFFUSION ALONG HORIZONTAL  }   PART
C                                      LEVELS AND ISOPICNALS  }
C IF:AZ=AR=BZ=BR=BZR=0.0         THEN DIFFUSION ALONG SIGMA LEVELS
C    AZ=BZ=1.0 AND AR=BR=BZR=0.0 THEN DIFFUSION ALONG HORIZONTAL LEVELS
C    AR=BR=1.0 AND AZ=BZ=BZR=0.0 THEN DIFFUSION ALONG ISOPICNALS
C    AR=BZR=1.0 AND AZ=BZ=BR=0.0 THEN ISOPICNAL DIFFUSION ALONG
C                                             HORIZONTAL LEVELS

      INCLUDE '0COM.INC'
C IMPLICIT - EXPLICIT CONTROL TRANSPORT T & S TIME SCHEME:
      INCLUDE '0TRTSS.INC'
      REAL  BZ, BZR
      REAL  FF(NX,NY,NZ),SLRX(NX,NY,NZ),AMX(NX,NY,NZ),TAU,AZ,AR
      REAL    AFNT(NX,NY,NZ+1), AFNV(NX,NY,NZ+1)
C SWEEP VARIABLES
      REAL A(NZ),B(NZ),C(NZ),ETA(NZ),RKSI(NZ)
C ADDITIONAL VARIABLES:
      REAL AMZX(NX,NZ+1), AMZZ(NX,NZ+1)
	REAL(8) SLH(NX,NY),SLH0(NX,NY)

C     REAL AMZX(NX,NZ+1)   ,AMZZ(NX,NZ+1),
C    &     AMZY(NY,NZ+1,NX),BMZZ(NY,NZ+1,NX)
C AMZY(NY,NZ+1,NX) -- ARRAY FOR COEFFICIENT OF DIFFUSIVE FLUXES
C                     IN Y-DIRECTION

C SWEEP BOUNDARIES
      INTEGER IFLAG,MBEG,MEND,MSTP,M,N,K
C HELP VARIABLES FOR INTERNAL CALCULATIONS
      REAL DCX1,DCX2,TDC,TDC0,AMUW,HLAMZ,HLAMP,ALFR,ALFZ
      REAL AFUNT, AFUNV, AFUNTP1, AFUNVP1, BLFR, BR
      REAL BLFZR, BLFZ
      INTEGER KFM1, KFP1
C--------------------------R O O L--------------------------------------

C ALLOCATE NEED ADDITIONAL VARIABLES:  	

C  BEGIN MAIN OPERATION LOOP

C  FLIP OR NOT DIRECTION OF DO-LOOP IN Y AND X DIRECTIONS
           IF(IFLAG.GT.0) THEN
              MBEG=MMM
              MEND=MM
              MSTP= 1
           ELSE
              MBEG=MM
              MEND=MMM
              MSTP=-1
           END IF

C    BOUNDARY CONTIONS ON TOP AND BOTTOM
               AMZX   =0.0
               AMZZ   =0.0
  
!$OMP PARALLEL DO 
!$OMP&PRIVATE(K,M,N,AFUNT,AFUNV,AFUNTP1,AFUNVP1,ALFR,ALFZ,AMUW,
!$OMP&     BLFR,BLFZ,BLFZR,HLAMZ,HLAMP,TDC,DCX1,DCX2,KFM1,KFP1,
!$OMP&     A,B,C,ETA,RKSI,TDC0)
!$OMP&FIRSTPRIVATE(AMZX,AMZZ)
      DO N=NNN,NN
C         DCX0  = RM(N)/(4.0*HX*HX)     !MAIN X-DIFUSUION COEFFICIENT

C  DEFINITION OF COEFFICIENTS FOR DIFFERENCE SHEMS
C  BOUNDARY CONTIONS ON LEFT AND RIGHT OF SWEEP ARE SATISFIED BY LCU MASK
C  BOUNDARY VALUES FOR PERIODIC CASE ARE SATISFIED BY PERIODICITY OF ARRAYS

C  UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG
C  HORIZONTAL AND ISOPICNALS LEVELS.
C  ADDITIONAL TERM IN Z-DIRECTION FOR DIFFUSION ALONG
C  HORIZONTAL LEVELS AND ISOPICNALS ARISEN FOR X-DIRECTION:
         DO K=2,NZ
            DO M=MMM-1,MM
C  DEFINITION OF COEFFICIENTS FOR FINITE DIFFERENCE SCHEME IN C-GRID POINTS
            IF(LCU(M,N).GT.0.5) THEN
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION

             AFUNT = AFNT(M,N,K)
             AFUNV = AFNV(M,N,K)

             AFUNTP1 = AFNT(M+1,N,K)
             AFUNVP1 = AFNV(M+1,N,K)

             ALFR=0.5*(AFUNT+AFUNTP1)
C  COEF. OF DIFFUSION IN W-POINTS AS FUNCTION ON Z:
             AMUW=0.25*(AMX(M,N,K-1)+AMX(M,N,K))*(AFUNV+AFUNVP1)
     &                *DYH(M,N)/DXT(M,N)/RN
               ALFZ=(1.0-ALFR)*AR+AZ
               BLFR=BR*ALFR
               BLFZ=(1.0-ALFR)*(BR+BZR)+BZ
              BLFZR=ALFR*BZR
               ALFR=ALFR*AR
C               HLAMZ=ZW(K)*(HHQ(M+1,N)- HHQ(M,N))    !HORIZONTAL
               HLAMZ=    (Z3D(M+1,N,K  )-Z3D(M,N,K  )
     &                   +Z3D(M+1,N,K-1)-Z3D(M,N,K-1))*0.5
               HLAMP=SLRX(M,N,K)                     !ISOPICNAL
               AMZX(M,K)=AMUW*(ALFZ*HLAMZ   +ALFR*HLAMP)
               AMZZ(M,K)=AMUW*(BLFZ*HLAMZ**2+BLFR*HLAMP**2+
     +                        BLFZR*HLAMZ   *     HLAMP)/
C     /                        (0.5*(HH (M,N-1)+HH (M,N))*HZT(K))
     /                        (HHU(M,N)*HZT(K))
            ELSE
               AMZX(M,K)=0.0E+00
               AMZZ(M,K)=0.0E+00
            END IF
            END DO
         END DO



C  BLOCK FOR Z-X PART OF CROSSDIFUUSION OPERATOR


         DO M=MBEG,MEND,MSTP
         IF (LU(M,N).GT.0.5) THEN

            TDC  = (HHQ(M,N)-SNGL(SLH (M,N)))*DX(M,N)*DY(M,N)*RN / TAU     !TIME DERIATIVE COEFFICIENT
            TDC0 = (HHQ(M,N)-SNGL(SLH0(M,N)))*DX(M,N)*DY(M,N)*RN / TAU
C  CHANGE DIFFERENCE APPROXIMATION ON THE BOUNDARIES IN X-DIRECTION
            IF(LCU(M-1,N).LT.0.5.OR.LCU(M,N).LT.0.5) THEN
               DCX1 = 0.5
            ELSE
               DCX1 = 0.25
            END IF

            DO  K=1,NZ
C  CHANGE INDEX FOR FF() ON THE SURFACE AND BOTTOM BOUNDARIES
            KFM1=MAX(1,K-1)
            KFP1=MIN(NZ,K+1)

               DCX2 = DCX1/DZ(K)

C   IN ABOVE OPERAND THE "1/HZT(K)" MAY BE CHANGED FOR
C   SECOND INDEX OF APPROXIMATION BY APPROPRIATE COMBINATION

C  FIRST  SWEEP COEFFICIENT:
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX AND AMZY
            A(K) = DCX2*(-AMZZ(M-1,K)  -AMZZ(M  ,K)
     +                   +AMZX(M  ,K)  -AMZX(M-1,K))

C  SECOND(CENTRAL) SWEEP COEFFICIENT:
            B(K) = TDC+DCX2*(
     +        -AMZX(M,K+1)  +AMZX(M,K)  +AMZX(M-1,K+1)  -AMZX(M-1,K)
     +        +AMZZ(M,K+1)  +AMZZ(M,K)  +AMZZ(M-1,K+1)  +AMZZ(M-1,K))

C  THIRD SWEEP COEFFICIENT:
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX AND AMZY
            C(K) = DCX2*(-AMZZ(M-1,K+1)  -AMZZ(M  ,K+1)
     +                   +AMZX(M-1,K+1)  -AMZX(M  ,K+1))


C  RIGHT HAND PART OF SWEEP (FULL CASE):
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX FROM CENTRAL (K) TERMS
          ETA(K) =  TDC0*FF(M,N,K)-DCX2*(
     +   +FF(M-1,N,KFM1)*( AMZX(M-1,K  )-AMZZ(M-1,K))
     +   +FF(M-1,N,K   )*(+AMZZ(M-1,K+1)+AMZZ(M-1,K)
     +                    -AMZX(M-1,K+1)+AMZX(M-1,K))
     +   +FF(M-1,N,KFP1)*(-AMZX(M-1,K+1)-AMZZ(M-1,K+1))
     +   +FF(M+1,N,KFM1)*(-AMZX(M ,K  ) -AMZZ(M ,K  ))
     +   +FF(M+1,N,K   )*(+AMZZ(M ,K  ) +AMZZ(M ,K+1)
     +                    -AMZX(M ,K  ) +AMZX(M ,K+1))
     +   +FF(M+1,N,KFP1)*( AMZX(M ,K+1) -AMZZ(M ,K+1)))

            END DO                       !END OF K-LOOP


         CALL FACTOR(NZ,A,B,C,ETA,RKSI,1,NZ)
            DO K=1,NZ
               FF(M,N,K) = RKSI(K)
            ENDDO

         ENDIF
         END DO                    !END OF M-LOOP

C  END OF BLOCK FOR Z-X PART OF CROSSDIFUUSION OPERATOR
      END DO                       !END OF N-LOOP
!$OMP END PARALLEL DO

      RETURN
      END
C=====================================================================
      SUBROUTINE TRDTYZ(FF,SLRY,AFNT,AFNV,VV,AMY,TAU,
     &                                IFLAG,AS,AZ,AR,SLH,SLH0)
      IMPLICIT NONE
C ADVECTION IS CRANK-NICOLSON, DIFFUSION IS PURE IMPLICIT.
C FOR TRANSPORT AND DIFFUSION OF PASSIVE COMPONENT
C UNIVERSAL COMPLEX DIFFUSION IN Y-DIRECTION AS SUMM OF DIFFUSION ALONG
C SIGMA, HORIZONTAL LEVELS AND ISOPICNALS
C MODIFIED BY DIANSKII N.A. 01.05.99 19:33
C Y-TRANSPORT AND DIFFUSION.
C Y-TRANSPORT SUPPOSES BOUNDARY VELOCITIES & FI TO BE ZERO.
C WATER BOUNDARIES AND SOLID BOUNDARIES ARE DIFFERENT.
C FF -- PASSIVE TRACER
C SLRY -- HH*Dy(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN Y-DIRECTION
C VV -- VELOCITY IN Y - DIRECTION
C FI -- STREAM FUNCTION FOR ADDING BAROTROPIC VELOCITY
C FBII,FBJJ -- VALUES OF FF ON SOUTH AND NORTH WALLS
C AMY -- COEFFICIENT OF DIFFUSION IN Y-DIRECTION
C TAU -- TIME STEP
C IGRYS,IGRYW -- DIRIHLET OR NEIMANN BOUNDARY CONDITION ON
C                SOUTH AND NORTH WALLS
C IFLAG -- CONTROL OF KIND OF DIFFUSION AND DIRECTION FOR K-LOOP
C          IF IFLAG>0 THEN K=1,NZ,+1 FOR MAIN K-LOOP
C          IF IFLAG<0 THEN K=NZ,1,-1 FOR MAIN K-LOOP
C AS -- WEIGHT COEFFICIENT FOR DIFFUSION ALONG SIGMA-LEVELS
C AZ -- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS
C AR -- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS
C IF:AS=1.0 AND AZ=AR=0.0 THEN DIFFUSION ALONG SIGMA LEVELS
C    AS=AZ=1.0 AND AR=0.0 THEN DIFFUSION ALONG HORIZONTAL LEVELS
C    AS=AR=1.0 AND AZ=0.0 THEN DIFFUSION ALONG ISOPICNALS

      INCLUDE '0COM.INC'
C IMPLICIT - EXPLICIT CONTROL TRANSPORT T & S TIME SCHEME:
      INCLUDE '0TRTSS.INC'

      DIMENSION FF(NX,NY,NZ),SLRY(NX,NY,NZ),
     &          VV(NX,NY,NZ),AMY(NX,NY,NZ)
	REAL      FF, VV, SLRY, AMY
      REAL      AFNT(NX,NY,NZ+1), AFNV(NX,NY,NZ+1)
	
C SWEEP VARIABLES
      REAL A(NY),B(NY),C(NY),ETA(NY),RKSI(NY)
C ADDITIONAL VARIABLES:
      REAL AMYY(NY,NZ+1),AMYZ(NY,NZ+1)
	REAL(8) SLH(NX,NY),SLH0(NX,NY)
C SWEEP BOUNDARIES
      INTEGER II,JJ,IFLAG,KBEG,KEND,KSTP,M,N,K
C HELP VARIABLES FOR INTERMEDIATE CALCULATIONS
      REAL  TAU,BP,BP0,DP,DM,PP,PM,DC1,AMUW,AS,AZ,AR,ALFR,ALFZ
      REAL  AFUNT, AFUNV, AFUNTP1, AFUNVP1
      INTEGER KFM1, KFP1, NSWEEP
C--------------------------R O O L--------------------------------------
      
C      DC0  = 1.0/(4.0*HY*HY)        !MAIN DIFUSUION COEFFICIENT
C      VVC  = 1.0/HY/2.0             !MAIN VELOSITY  COEFFICIENT
C  BEGIN MAIN ARRAY LOOP
C    BOUNDARY CONTIONS ON TOP AND BOTTOM
                  AMYZ=0.0
                  AMYY=0.0

C         TO FLIP OR NOT DIRECTION OF DO CICLE ON Z
                    IF(IFLAG.GT.0) THEN
                        KBEG=1
                        KEND=NZ
                        KSTP=1
                     ELSE
                        KBEG=NZ
                        KEND=1
                        KSTP=-1
                     END IF

!$OMP PARALLEL DO 
!$OMP&PRIVATE(K,M,N,AFUNT,AFUNV,AFUNTP1,AFUNVP1,ALFR,ALFZ,AMUW,
!$OMP&     DC1,KFM1,KFP1,NSWEEP,II,JJ,
!$OMP&     BP,BP0,DM,DP,PM,PP,A,B,C,ETA,RKSI)
!$OMP&FIRSTPRIVATE(AMYY,AMYZ)
      DO M=MMM,MM
C  DEFINITION OF COEFFICIENTS FOR DIFFERENCE SHEMS
C  BOUNDARY CONTIONS ON LEFT AND RIGHT OF SWEEP ARE SATISFIED BY LCV MASK
C  DEFINITION IN INNER DOMAIN

C  UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG SIGMA,
C  HORIZONTAL LEVELS AND ISOPICNALS
         DO K=2,NZ
            DO N=NNN-1,NN
C  DEFINITION OF COEFFICIENTS FOR FINITE DIFFERENCE SCHEME IN C-GRID POINTS
C  TO DEFINE COEFF. WITHOUT IF STATEMENT DEFINE AMUW SO:
C                 AMUW=0.5*(AMY(N,K-1)+AMY(N,K))*RN*RN/RM1(N)*LCV(M,N)
               IF(LCV(M,N).GT.0.5) THEN
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION
 
                  AFUNT = AFNT(M,N,K)
                  AFUNV = AFNV(M,N,K)

                  AFUNTP1 = AFNT(M,N+1,K)
                  AFUNVP1 = AFNV(M,N+1,K)

                  ALFR=0.5*(AFUNT+AFUNTP1)
                  ALFZ=(1.0-ALFR)*AR+AZ
                  ALFR=ALFR*AR
C  COEF. OF DIFFUSION IN W-POINTS AS FUNCTION ON Z:
                  AMUW=0.25*(AMY(M,N,K-1)+AMY(M,N,K))
     &                *DXH(M,N)/DYT(M,N)/RN
     &                *(AFUNV+AFUNVP1)
C  DIFFUSION ALONG SIGMA LEVELS:
                  AMYY(N,K)=AS*AMUW*HZT(K)*HHV(M,N)
C                 AMYY(N,K)=AS*AMUW*HZT(K)*0.5*(HH (M-1,N)+HH (M,N))
C  ADDITIONAL TERM FOR DIFFUSION ALONG ISOPICNALS AND HORIZONTAL LEVELS:
                  AMYZ(N,K)=AMUW*(SLRY(M,N,K)*ALFR+
     +               ALFZ* 
     &                   (Z3D(M,N+1,K  )-Z3D(M,N,K  )
     &                   +Z3D(M,N+1,K-1)-Z3D(M,N,K-1))*0.5)
      
C    &               ZW(K)*( HHQ(M,N+1)-HHQ(M,N)))

               ELSE
                  AMYY(N,K)=0.0E+00
                  AMYZ(N,K)=0.0E+00
               END IF
            END DO
         END DO


C  BLOCK FOR Y-Z PART OF CROSSDIFUUSION OPERATOR

            DO  K=KBEG,KEND,KSTP

C  CHANGE DIFFERENCE APPROXIMATION AND INDEX FOR FF()
C                                ON THE SURFACE AND BOTTOM BOUNDARIES
            IF(K.EQ.1.OR.K.EQ.NZ)  THEN
               DC1 = 0.5/DZ(K)
            ELSE
               DC1 = 0.25/DZ(K)
            END IF
            
            KFM1=MAX(1,K-1)
            KFP1=MIN(NZ,K+1)

C   IN ABOVE "IF-THEN-ELSE" THE "1/HZT(K)" MAY BE CHANGED FOR
C   SECOND INDEX OF APPROXIMATION BY APPROPRIATE COMBINATION

                        DO  NSWEEP=1,LRY(M)

         II = IIY(NSWEEP,M)
         JJ = JJY(NSWEEP,M)
           DO N=II,JJ

          BP = (HHQ(M,N)-SNGL(SLH (M,N)))*DX(M,N)*DY(M,N)*RN / TAU       !TIME DERIVATIVE COEFFICIENT
          BP0= (HHQ(M,N)-SNGL(SLH0(M,N)))*DX(M,N)*DY(M,N)*RN / TAU
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMYZ
           DM = DC1*(AMYY(N-1,K)+AMYY(N-1,K+1)         !DIFFUSION
     +              +AMYZ(N-1,K)-AMYZ(N-1,K+1))        !CROSS DIFFUSION
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMYZ
           DP = DC1*(AMYY(N,K)+AMYY(N,K+1)             !DIFFUSION
     -              -AMYZ(N,K)+AMYZ(N,K+1))            !CROSS DIFFUSION
C          PP = ( RN * VV(M,N  ,K)*(HH(M,N  )+HH(M-1,N))/2./RM1(N)
C     -              )*VVC
C          PM = ( RN * VV(M,N-1,K)*(HH(M,N-1)+HH(M-1,N-1))/2./RM1(N-1)
C     -              )*VVC
          PP =  VV(M,N  ,K)*DXH(M,N  )
     &        *HHV(M,N  )/2.0

          PM =  VV(M,N-1,K)*DXH(M,N-1)
     &        *HHV(M,N-1)/2.0
C LAST "2" ABOVE IS DUE TO APROXIMATION IN SPACE.
C  FIRST SWEEP COEFFICIENT:
          A(N) = -PM * TRTS_IMP - DM
C  THIRD SWEEP COEFFICIENT:
          C(N) =  PP * TRTS_IMP - DP
C  SECOND(CENTRAL) SWEEP COEFFICIENT:
          B(N) =  BP
     +            + DC1*( AMYY(N-1,K)+AMYY(N-1,K+1)
     +                   +AMYY(N  ,K)+AMYY(N  ,K+1)
     -                   -AMYZ(N-1,K)+AMYZ(N-1,K+1)
     +                   +AMYZ(N  ,K)-AMYZ(N  ,K+1) )

C  RIGHT HAND PART OF SWEEP (FULL CASE):
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMYZ FROM CENTRAL (M) TERMS
          ETA(N) = BP0* FF(M,N,K) - PP * TRTS_EXP * FF(M,N+1,K)
     +                            + PM * TRTS_EXP * FF(M,N-1,K)
     +           +  DC1*(
     +   +FF(M,N-1,KFM1)*( AMYY(N-1,K  )-AMYZ(N-1,K))
     +   +FF(M  ,N,KFM1)*(-AMYY(N-1,K  )-AMYY(N  ,K)
     +                    -AMYZ(N-1,K  )+AMYZ(N  ,K))
     +   +FF(M,N+1,KFM1)*( AMYY(N  ,K  )+AMYZ(N  ,K))
     +   +FF(M,N-1,KFP1)*( AMYY(N-1,K+1)+AMYZ(N-1,K+1))
     +   +FF(M  ,N,KFP1)*(-AMYY(N-1,K+1)-AMYY(N  ,K+1)
     +                    +AMYZ(N-1,K+1)-AMYZ(N  ,K+1))
     +   +FF(M,N+1,KFP1)*( AMYY(N  ,K+1)-AMYZ(N  ,K+1)))

         END DO                       !END OF M-LOOP X-Z DIRECTION

C IMPLICIT: A(N)=-DM-PM;C(N)=-DP+PP;B(N)=BP+DM+DP;ETA(N)=BP*FF(M,N,K)

         CALL FACTOR(NY,A,B,C,ETA,RKSI,II,JJ)
         DO  N=II,JJ
          FF(M,N,K) = RKSI(N)
         END DO
                            END DO    !END OF NSWEEP-LOOP Y-Z DIRECTION
         END DO                       !END OF K-LOOP
C  END OF BLOCK FOR Y-Z PART OF CROSSDIFUUSION OPERATOR

      END DO                          !END OF M-LOOP
!$OMP END PARALLEL DO
      
      RETURN
      END
C=====================================================================72
      SUBROUTINE TRDTZY(FF,SLRY,AFNT,AFNV,
     &                   AMY,TAU,IFLAG,AZ,AR,BZ,BR,BZR,SLH,SLH0)
      IMPLICIT NONE
C FOR TRANSPORT AND DIFFUSION OF PASSIVE COMPONENT
C UNIVERSAL COMPLEX PART OF DIFFUSION IN Z-DIRECTION AS SUMM OF
C DIFFUSION ALONG HORIZONTAL LEVELS AND ISOPICNALS
C MODIFIED BY DIANSKII N.A. 01.05.99 19:17
C X-TRANSPORT AND DIFFUSION.
C X-TRANSPORT SUPPOSES BOUNDARY VELOCITIES & FI TO BE ZERO.
C FF -- PASSIVE TRACER
C SLRX -- HH*Dx(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN X-DIRECTION
C AMX -- COEFFICIENT OF DIFFUSION IN X-DIRECTION
C SLRY -- HH*Dy(DENSITY)/(dDENSITY/dZ) - ISOPICNAL SLOPE IN Y-DIRECTION
C AMY -- COEFFICIENT OF DIFFUSION IN Y-DIRECTION
C TAU -- TIME STEP
C IFLAG -- CONTROL OF KIND OF DIFFUSION AND DIRECTION FOR K-LOOP
C          IF IFLAG>0 THEN M=MMM,MM ,+1;N=NNN,NN, +1 FOR MAIN N-LOOP
C          IF IFLAG<0 THEN M=MM ,MMM,-1;N=NN ,NNN,-1 FOR MAIN N-LOOP
C AZ- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS]IN FIRST
C AR- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS       ]   PART
C BZ- WEIGHT COEFFICIENT FOR DIFFUSION ALONG HORIZONTAL LEVELS}
C BR- WEIGHT COEFFICIENT FOR DIFFUSION ALONG ISOPICNALS       }IN SECOND
C BZR-WEIGHT COEFFICIENT FOR MIXT DIFFUSION ALONG HORIZONTAL  }   PART
C                                      LEVELS AND ISOPICNALS  }
C IF:AZ=AR=BZ=BR=BZR=0.0         THEN DIFFUSION ALONG SIGMA LEVELS
C    AZ=BZ=1.0 AND AR=BR=BZR=0.0 THEN DIFFUSION ALONG HORIZONTAL LEVELS
C    AR=BR=1.0 AND AZ=BZ=BZR=0.0 THEN DIFFUSION ALONG ISOPICNALS
C    AR=BZR=1.0 AND AZ=BZ=BR=0.0 THEN ISOPICNAL DIFFUSION ALONG
C                                             HORIZONTAL LEVELS

      INCLUDE '0COM.INC'
C IMPLICIT - EXPLICIT CONTROL TRANSPORT T & S TIME SCHEME:
      INCLUDE '0TRTSS.INC'
      REAL  BZ, BZR
      REAL  FF(NX,NY,NZ),SLRY(NX,NY,NZ),AMY(NX,NY,NZ),TAU,AZ,AR
      REAL    AFNT(NX,NY,NZ+1), AFNV(NX,NY,NZ+1)
C SWEEP VARIABLES
      REAL A(NZ),B(NZ),C(NZ),ETA(NZ),RKSI(NZ)
C ADDITIONAL VARIABLES:

      REAL  AMZY(NY,NZ+1), BMZZ(NY,NZ+1)
	REAL(8) SLH(NX,NY),SLH0(NX,NY)
C     REAL AMZX(NX,NZ+1)   ,AMZZ(NX,NZ+1),
C    &     AMZY(NY,NZ+1,NX),BMZZ(NY,NZ+1,NX)
C AMZY(NY,NZ+1,NX) -- ARRAY FOR COEFFICIENT OF DIFFUSIVE FLUXES
C                     IN Y-DIRECTION

C SWEEP BOUNDARIES
      INTEGER IFLAG,M,N,K
C HELP VARIABLES FOR INTERNAL CALCULATIONS
      REAL TDC,TDC0,AMUW,HLAMZ,HLAMP,ALFR,ALFZ
      REAL AFUNT, AFUNV, AFUNTP1, AFUNVP1, DCY1, DCY2, BLFR, BR
      REAL BLFZR, BLFZ
      INTEGER KFM1, KFP1, NEND, NSTP, NBEG
C--------------------------R O O L--------------------------------------

C ALLOCATE NEED ADDITIONAL VARIABLES:  	

C    BOUNDARY CONTIONS ON TOP AND BOTTOM

               AMZY=0.0
               BMZZ=0.0

C  FLIP OR NOT DIRECTION OF DO-LOOP IN Y AND X DIRECTIONS
           IF(IFLAG.GT.0) THEN
                  NBEG=NNN
                  NEND=NN
                  NSTP=1
           ELSE
                  NBEG=NN
                  NEND=NNN
                  NSTP=-1
           END IF

!$OMP PARALLEL DO 
!$OMP&PRIVATE(K,M,N,AFUNT,AFUNV,AFUNTP1,AFUNVP1,ALFR,ALFZ,AMUW,
!$OMP&     BLFR,BLFZ,BLFZR,HLAMZ,HLAMP,TDC,DCY1,DCY2,KFM1,KFP1,
!$OMP&     A,B,C,ETA,RKSI,TDC0)
!$OMP&FIRSTPRIVATE(AMZY,BMZZ)
      DO M=MMM,MM

C  DEFINE ARRAY OF COEFFICIENTS FOR Z-Y DIRECTION

C  DEFINITION OF COEFFICIENTS FOR DIFFERENCE SHEMS
C  BOUNDARY CONTIONS ON LEFT AND RIGHT OF SWEEP ARE SATISFIED BY LCV MASK
C  DEFINITION IN INNER DOMAIN

C  UNIVERSAL COMPLEX DIFFUSION AS SUMM OF DIFFUSION ALONG
C  HORIZONTAL AND ISOPICNALS LEVELS.
C  ADDITIONAL TERM IN Z-DIRECTION FOR DIFFUSION ALONG
C  HORIZONTAL LEVELS AND ISOPICNALS ARISEN FOR Y-DIRECTION:
         DO K=2,NZ
            DO N=NNN-1,NN
C  DEFINITION OF COEFFICIENTS FOR FINITE DIFFERENCE SCHEME IN C-GRID POINTS
            IF(LCV(M,N).GT.0.5) THEN
C  WEIGHT COEFFICIENTS FOR Z-AND-ISOPICNAL DIFFUSION
             AFUNT = AFNT(M,N,K)
             AFUNV = AFNV(M,N,K)
              
             AFUNTP1 = AFNT(M,N+1,K)
             AFUNVP1 = AFNV(M,N+1,K)

             ALFR=0.5*(AFUNT+AFUNTP1)
C  COEF. OF DIFFUSION IN W-POINTS AS FUNCTION ON Z:
             AMUW=0.25*(AMY(M,N,K-1)+AMY(M,N,K))
     &                *DXH(M,N)/DYT(M,N)/RN                    
     &                *(AFUNV+AFUNVP1)
               ALFZ=(1.0-ALFR)*AR+AZ
               BLFR=BR*ALFR
               BLFZ=(1.0-ALFR)*(BR+BZR)+BZ
              BLFZR=ALFR*BZR
               ALFR=ALFR*AR
               HLAMZ=    (Z3D(M,N+1,K  )-Z3D(M,N,K  )
     &                   +Z3D(M,N+1,K-1)-Z3D(M,N,K-1))*0.5
C               HLAMZ=ZW(K)*(HHQ(M,N+1)-HHQ(M,N))    !HORIZONTAL
               HLAMP=SLRY(M,N,K)                    !ISOPICNAL
               AMZY(N,K)=AMUW*(ALFZ*HLAMZ   +ALFR*HLAMP)
               BMZZ(N,K)=AMUW*(BLFZ*HLAMZ**2+BLFR*HLAMP**2
     +                         +BLFZR*HLAMZ   *     HLAMP)/
C    /                  (0.5*(HH (M-1,N)+HH (M,N))*HZT(K))
     /                  (HHV(M,N)*HZT(K))
            ELSE
               AMZY(N,K)=0.0E+00
               BMZZ(N,K)=0.0E+00
            END IF
            END DO
         END DO

C  BEGIN MAIN OPERATION LOOP

C         DCY0  = 1.0/(4.0*HY*HY)       !MAIN Y-DIFUSUION COEFFICIENT

      DO N=NBEG,NEND,NSTP
 
C  BLOCK FOR Z-Y PART OF CROSSDIFUUSION OPERATOR

          IF (LU(M,N).GT.0.5) THEN

            TDC  = (HHQ(M,N)-SNGL(SLH (M,N)))*DX(M,N)*DY(M,N)*RN / TAU   !TIME DERIATIVE COEFFICIENT
            TDC0 = (HHQ(M,N)-SNGL(SLH0(M,N)))*DX(M,N)*DY(M,N)*RN / TAU
C  CHANGE DIFFERENCE APPROXIMATION ON THE BOUNDARIES IN Y-DIRECTION
            IF(LCV(M,N-1).LT.0.5.OR.LCV(M,N).LT.0.5)  THEN
               DCY1 = 0.5
            ELSE
               DCY1 = 0.25
            END IF

            DO  K=1,NZ

C  CHANGE INDEX FOR FF() ON THE SURFACE AND BOTTOM BOUNDARIES
            KFM1=MAX(1,K-1)
            KFP1=MIN(NZ,K+1)

               DCY2 = DCY1/DZ(K)

C   IN ABOVE OPERAND THE "1/HZT(K)" MAY BE CHANGED FOR
C   SECOND INDEX OF APPROXIMATION BY APPROPRIATE COMBINATION

C  FIRST  SWEEP COEFFICIENT:
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX AND AMZY
            A(K) = DCY2*(-BMZZ(N-1,K)-BMZZ(N  ,K)
     +                   +AMZY(N  ,K)-AMZY(N-1,K))

C  SECOND(CENTRAL) SWEEP COEFFICIENT:
            B(K) = TDC +  DCY2*(
     +        -AMZY(N,K+1)+AMZY(N,K)+AMZY(N-1,K+1)-AMZY(N-1,K)
     +        +BMZZ(N,K+1)+BMZZ(N,K)+BMZZ(N-1,K+1)+BMZZ(N-1,K))

C  THIRD SWEEP COEFFICIENT:
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX AND AMZY
            C(K) = DCY2*(-BMZZ(N-1,K+1)-BMZZ(N  ,K+1)
     +                   +AMZY(N-1,K+1)-AMZY(N  ,K+1))

C  RIGHT HAND PART OF SWEEP (FULL CASE):
C  VARIANT WITH ANNIHILATION TERMS - REMOVE AMZX FROM CENTRAL (K) TERMS
          ETA(K) =  TDC0*FF(M,N,K) - DCY2*(
     +   +FF(M,N-1,KFM1)*( AMZY(N-1,K  )-BMZZ(N-1,K  ))
     +   +FF(M,N-1,K   )*(+BMZZ(N-1,K+1)+BMZZ(N-1,K  )
     +                    -AMZY(N-1,K+1)+AMZY(N-1,K  ))
     +   +FF(M,N-1,KFP1)*(-AMZY(N-1,K+1)-BMZZ(N-1,K+1))
     +   +FF(M,N+1,KFM1)*(-AMZY(N  ,K  )-BMZZ(N  ,K  ))
     +   +FF(M,N+1,K   )*(+BMZZ(N  ,K  )+BMZZ(N  ,K+1)
     +                    -AMZY(N  ,K  )+AMZY(N  ,K+1))
     +   +FF(M,N+1,KFP1)*( AMZY(N  ,K+1)-BMZZ(N  ,K+1)))


            END DO                       !END OF K-LOOP


         CALL FACTOR(NZ,A,B,C,ETA,RKSI,1,NZ)
            DO K=1,NZ
               FF(M,N,K) = RKSI(K)
            ENDDO

         ENDIF
         END DO                    !END OF M-LOOP

C  END OF BLOCK FOR Z-X PART OF CROSSDIFUUSION OPERATOR

      END DO                       !END OF N-LOOP
!$OMP END PARALLEL DO

      RETURN
      END
C=======================================================================
